apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'
apply plugin: 'com.jakewharton.butterknife'

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.ftacloud.student"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        // 使用高斯模糊
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true

        multiDexEnabled true
        flavorDimensions "versionCode" // 多维度打包

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Android Studio pluing 3.0.+ 以上 ，如果项目中用到注解，必须显示的声明注释处理器
        javaCompileOptions {
            annotationProcessorOptions {
                // 数据库导出
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }


    productFlavors {
        dev {
            buildConfigField "String", "SERVER_HOST", '"http://192.168.1.144:52001"'
            buildConfigField "String", "H5_BASE_URL", '"https://www.baidu.com"'
        }

        formal {
            buildConfigField "String", "SERVER_HOST", '"https://www.baidu.com"'
            buildConfigField "String", "H5_BASE_URL", '"https://www.baidu.com"'
        }

    }
    signingConfigs {
        debug { storeFile file("../keystore/debug.keystore") }
        release {
            storeFile file('../keystore/educationStudent.jks')
            storePassword 'cloud.33'
            keyAlias 'educationStudent'
            keyPassword 'cloud.33'
        }

    }


    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        release {
            debuggable true
            minifyEnabled false // 是否运行混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // 忽略lit提示
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'MissingTranslation'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Student_v${defaultConfig.versionCode}_(${defaultConfig.versionName})_${releaseTime()}.apk"
        }
    }

}

androidExtensions {
    // 替代 findViewById
    experimental = true
}


dependencies {
    api fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':sugar')


    // butterknife 已经在sugar 中引入，需要加上 kapt. model使用时，使用R2 替代R
    kapt 'com.jakewharton:butterknife-compiler:10.2.0'

    // Dagger 2 支持
    kapt "com.google.dagger:dagger-compiler:2.19"
    kapt "com.google.dagger:dagger-android-processor:2.19"
    kapt 'androidx.room:room-compiler:2.2.5'

    // 百家云SDK(带UI)  ，需要更改代码时，改成project 依赖https://dev.baijiayun.com/wiki/detail/93
    implementation 'com.baijiayun.live:group-class-ui:1.1.1'

    // 百家云SDK自1.1.2版本开始，引入了kotlin实现的第三方依赖库。需要额外配置kotlin版本。
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko-commons:0.10.8"

    // 流式布局zhy
    implementation 'com.hyman:flowlayout-lib:1.1.2'

    // 通用popup window
    implementation 'com.github.razerdp:BasePopup:2.2.8'

    // # 下载 #
    implementation 'com.liulishuo.filedownloader:library:1.7.7'

    // 横向recycleView
    implementation 'com.yc:PagerLib:1.0.1'

}
